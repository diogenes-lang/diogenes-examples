package it.unica.co2.auto.jlamp
honesty P

/*
 * CONTRACTS
 */
contract CB {
	clickPay? . pay? string
	+ clickVoucher? . (
		reject! . pay? string (+) accept! . voucher? string
	)
}

contract CV {
	ok? + no?
}

/* 
 * PROCESSES
 */
process P {
	(x:session) telll x CB . ask x . (
		do x clickPay? . do x pay? code:string 
		+ do x clickVoucher? . Q(x)
	)
}

process Q (x:session) {
	(y:session) telll y CV . (
		ask y . Q1(x,y)
		+ t . (
			AbortX(x) | ask y . AbortY(y)
		)
	)
}

process Q1 (x:session, y:session) {
	do y ok? . do x accept! . do x voucher? v:string
	+ do y no? . do x reject! . do x pay? code:string
	+ t. R1(x,y)	
}

process R1 (x:session, y:session){
	AbortX(x) | AbortY(y)
}

process AbortX (x:session){
	do x reject! . do x pay? code:string
}

process AbortY (y:session){
	do y ok? + do y no?
}
